class Bytecode {
	def writer:     DataWriter     = DataWriter.new();

	var bytecode:   Array<byte>;
	def strings:    Vector<string> = Vector.new();
	def classholes: Vector<ClassObject -> ClassObject>  = Vector.new();
	def constants:  Vector<Object> = Vector.new();

	/* Source mappings */
	def source: Vector<FileRange>  = Vector.new();

	new(ast: AST) {
		compileAST(ast);
		bytecode = writer.copy();
	}

	def compileAST(ast: AST) {
		def loc = ast.loc;
		match (ast.data) {
			Lit(obj) => {
				source.put(loc);
				putb(Opcode.LOAD_LIT.tag);
				putO(addConstant(obj));
			}
			Id(id) => {
				source.put(loc);
				putb(Opcode.LOAD_NAME.tag);
				putO(addString(id));
			}
			Apply(tgt, args) => {
				compileAST(tgt);
				source.put(loc);
				putb(Opcode.ASSERT_FUNC.tag);
				zeroO();
				for (arg in args) {
					compileAST(arg);
				}
				source.put(loc);
				putb(Opcode.CALL.tag);
				putO(args.length);
			}
			If(cond, tru, fals) => {
				compileAST(cond);
				source.put(loc);
				putb(Opcode.JUMP_IF_FALSE.tag);
				def hole1 = pos();
				skipO(); // hole to put jump offset
				compileAST(tru);
				source.put(loc);
				putb(Opcode.JUMP.tag);
				def hole2 = pos();
				skipO(); // hole to put jump offset
				writeO(hole1, pos() - (hole1 + 3));
				compileAST(fals);
				writeO(hole2, pos() - (hole2 + 3));
			}
			Cond(cases) => {
				def ends: Vector<int> = Vector.new();
				for (c in cases) {
					def cond = c.0;
					def body = c.1;

					compileAST(cond);
					source.put(loc);
					putb(Opcode.JUMP_IF_FALSE.tag);
					def hole = pos();
					skipO(); // hole to put jump offset
					compileAST(body);
					source.put(loc);
					putb(Opcode.JUMP.tag);
					ends.put(pos());
					skipO(); // hole to put jump offset
					writeO(hole, pos() - (hole + 3));
				}
				def idx = constants.length;
				// XXX object creation in AST compilation!
				// could be problematic if we object lookup observable
				source.put(loc);
				putb(Opcode.LOAD_LIT.tag);
				putO(addConstant(StrObjects.getStrObject("cond error: no true breanch")));
				source.put(loc);
				putb(Opcode.RAISE.tag);
				putO(0); // no operand
				// fill in all jump to end targets
				for (hole in ends.extract()) {
					writeO(hole, pos() - (hole + 3));
				}
			}
			Func(id, params, code) => {
				// XXX compiling code
				code.compile();
				
				// XXX object creation in AST compilation!
				def cst = addConstant(FuncObjects.newFO(id, params, code));
				def str = addString(id);
			
				source.put(loc);
				putb(Opcode.LOAD_LIT.tag);
				putO(cst);
				source.put(loc);
				putb(Opcode.STORE_NAME.tag);
				putO(str);
				source.put(loc);
				putb(Opcode.LOAD_NAME.tag);
				putO(str);
			}
			Assign(id, code) => {
				compileAST(code);
				
				def str = addString(id);
				source.put(loc);
				putb(Opcode.STORE_NAME.tag);
				putO(str);
				source.put(loc);
				putb(Opcode.LOAD_NAME.tag);
				putO(str);
			}
			Raise(exn) => {
				compileAST(exn);
				source.put(loc);
				putb(Opcode.RAISE.tag);
				putO(0); // no operand
			}
			Catch(try, id, catch) => {
				source.put(loc);
				putb(Opcode.TRY.tag);
				def hole1 = pos();
				skipO();
				compileAST(try);
				source.put(loc);
				putb(Opcode.CATCH.tag);
				putO(0); // no operand
				source.put(loc);
				putb(Opcode.JUMP.tag);
				def hole2 = pos();
				skipO();
				writeO(hole1, pos() - (hole1 + 3));

				source.put(loc);
				putb(Opcode.STORE_NAME.tag);
				putO(addString(id));

				compileAST(catch);
				writeO(hole2, pos() - (hole2 + 3));
			}
			Begin(exprs) => {
				for (i < exprs.length - 1) {
					compileAST(exprs[i]);
					source.put(loc);
					putb(Opcode.PULL.tag);
					putO(1); // ignore eval result
				}
				compileAST(exprs[exprs.length - 1]);
			}
			While(cond, body) => {
				def head = pos();
				compileAST(cond);
				source.put(loc);
				putb(Opcode.JUMP_IF_FALSE_PEEK.tag);
				def hole = pos();
				skipO(); // hole to put jump offset
				compileAST(body);
				source.put(loc);
				putb(Opcode.PULL.tag);
				putO(1);
				source.put(loc);
				putb(Opcode.JUMP.tag);
				skipO();
				writeO(pos() - 3, head - pos());
				writeO(hole, pos() - (hole + 3));
			}
			GetField(id, obj) => {
				compileAST(obj);
				source.put(loc);
				putb(Opcode.GET_FIELD.tag);
				putO(addString(id));
			}
			SetField(id, obj, val) => {
				compileAST(obj);
				compileAST(val);
				source.put(loc);
				putb(Opcode.SET_FIELD.tag);
				putO(addString(id));
			}
			Class(id, superclass, init, fields, methods) => {
				// there are subcodes which are getting compiled here
				// TODO should this compilation process just be part of code creation?
				match (superclass) {
					Nothing => {
						def idx = constants.length;
						source.put(loc);
						putb(Opcode.LOAD_LIT.tag);
						putO(addConstant(Objects.classBase));
					}
					Just(expr) => compileAST(expr);
				}
				var init2: Code;
				match (init) {
					Nothing => ;
					Just(code) => {
						code.compile();
						init2 = code;
					}
				}
				//                                   XXX
				def idx = addClasshole(ClassObjects.classhole(id, init2, fields, Arrays.map(methods, funcToFunc)));
				source.put(loc);
				putb(Opcode.CREATE_CLASS.tag);
				putO(idx);
				def str = addString(id);
				source.put(loc);
				putb(Opcode.STORE_NAME.tag);
				putO(str);
				source.put(loc);
				putb(Opcode.LOAD_NAME.tag);
				putO(str);
			}
			New(obj) => {
				compileAST(obj);
				source.put(loc);
				putb(Opcode.NEW.tag);
				putO(0); // no operand
			}
			List(elems) => {
				for (elem in elems)
					compileAST(elem);
				putb(Opcode.CREATE_LIST.tag);
				putO(elems.length);
			}

//			_ => System.error("compile error", "unsupported AST node");
		}
		
	}

	// wrappers around datawriter
	def putb(b: int)  { writer.putb(b); }
	def putO(o: int)  { writer.putbbb(o, o >> 8, o >> 16); }
	//def skipN(s: int) { writer.skipN(s); }
	def skipO()       { writer.skipN(3); }
	//def zeroN(s: int) { writer.zeroN(s); }
	def zeroO()       { writer.zeroN(3); }
	def pos() -> int  { return writer.pos; }
	//def writeb(addr: int, data: int) { writer.data[addr] = byte.view(data); }
	def writeO(addr: int, data: int) {
		writer.data[addr]     = byte.view(data);
		writer.data[addr + 1] = byte.view(data >> 8);
		writer.data[addr + 2] = byte.view(data >> 16);
	}

	def addString(str: string) -> int {
		def idx = strings.length;
		strings.put(str);
		return idx;
	}

	def addClasshole(clazz: ClassObject -> ClassObject) -> int {
		def idx = classholes.length;
		classholes.put(clazz);
		return idx;
	}

	def addConstant(const: Object) -> int {
		def idx = constants.length;
		constants.put(const);
		return idx;
	}

	/* print this bytecode */
	def inspect() {
		def bc = bytecode;
		var pc = 0;
		while (pc < bc.length) {
			def bc      = Ref<Instruction>.at(bc, pc);
			def opcode  = bc.opcode;
			def operand = bc.operand;
			def loc = source[pc >> 2];

			System.puts(Strings.format3("+%d: %s %d", pc, opcode.name, operand));
			System.puts(Strings.format2(" (%d:%d)\n", loc.beginLine, loc.beginColumn));
			
			pc += 4;
			// dispatch on bytecode
			match (opcode) {
				_ => ;
			}
		}

	}
}

layout Instruction {
	+0  opcode:  Opcode;
	+1  operand: u24; // you may treat this as unsigned or signed depending on opcode
	=4;
}

component Bytecodes {
	def inspect(code: Array<byte>) {
		var pc = 0;
		while (pc < code.length) {
			def bytecode = Ref<Instruction>.at(code, pc);
			def opcode   = bytecode.opcode;
			def operand  = bytecode.operand;

			System.puts(Strings.format3("+%d: %s %d", pc, opcode.name, operand));
			
			pc += 4;
			// dispatch on bytecode
			match (opcode) {
				_ => ;
			}
		}
	}
}


// def main(args: Array<string>) -> int {
// 	def code: Array<byte> = [0, 0, 0, 1, 0, 2, 1, byte.view(-3)];
// 
// 	inspect(code);
// 
// 	return 0;
// }
def funcToFunc(f: ASTData.Func) -> FuncObject {
	return FuncObjects.newFO(f.id, f.params, f.body);
}

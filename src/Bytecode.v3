class Bytecode {
	def bytecode:   DataWriter     = DataWriter.new();
	def strings:    Vector<string> = Vector.new();
	def classholes: Vector<Class>  = Vector.new(); // XXX these are not real classes!
	def constants:  Vector<Object> = Vector.new();

	/* Source mappings */
	def source: Vector<FileRange>  = Vector.new();

	new(ast: AST) {
		compileAST(ast);
	}

	def compileAST(ast: AST) {
		def loc = ast.loc;
		match (ast.data) {
			Lit(obj) => {
				source.put(loc);
				putb(Opcode.LOAD_LIT.tag);
				putb(addConstant(obj));
			}
			Id(id) => {
				source.put(loc);
				putb(Opcode.LOAD_NAME.tag);
				putb(addString(id));
			}
			Apply(tgt, args) => {
				compileAST(tgt);
				source.put(loc);
				putb(Opcode.ASSERT_FUNC.tag);
				zeroN(1);
				for (arg in args) {
					compileAST(arg);
				}
				source.put(loc);
				putb(Opcode.CALL.tag);
				putb(args.length);
			}
			If(cond, tru, fals) => {
				compileAST(cond);
				source.put(loc);
				putb(Opcode.JUMP_IF_FALSE.tag);
				def hole1 = pos();
				skipN(1); // hole to put jump offset
				compileAST(tru);
				source.put(loc);
				putb(Opcode.JUMP.tag);
				def hole2 = pos();
				skipN(1); // hole to put jump offset
				writeb(hole1, pos() - hole1 - 1);
				compileAST(fals);
				writeb(hole2, pos() - hole2 - 1);
			}
			Cond(cases) => {
				def ends: Vector<int> = Vector.new();
				for (c in cases) {
					def cond = c.0;
					def body = c.1;

					compileAST(cond);
					source.put(loc);
					putb(Opcode.JUMP_IF_FALSE.tag);
					def hole = pos();
					skipN(1); // hole to put jump offset
					compileAST(body);
					source.put(loc);
					putb(Opcode.JUMP.tag);
					ends.put(pos());
					skipN(1); // hole to put jump offset
					writeb(hole, pos() - hole - 1);
				}
				def idx = constants.length;
				// XXX object creation in AST compilation!
				// could be problematic if we object lookup observable
				source.put(loc);
				putb(Opcode.LOAD_LIT.tag);
				putb(addConstant(StrObjects.getStrObject("cond error: no true breanch")));
				source.put(loc);
				putb(Opcode.RAISE.tag);
				putb(0); // no operand
				// fill in all jump to end targets
				for (hole in ends.extract()) {
					writeb(hole, pos() - hole - 1);
				}
			}
			Func(id, params, code) => {
				// XXX compiling code
				code.compile();
				
				// XXX object creation in AST compilation!
				def cst = addConstant(FuncObjects.newFO(Func.new(id, params, code)));
				def str = addString(id);
			
				source.put(loc);
				putb(Opcode.LOAD_LIT.tag);
				putb(cst);
				source.put(loc);
				putb(Opcode.STORE_NAME.tag);
				putb(str);
				source.put(loc);
				putb(Opcode.LOAD_NAME.tag);
				putb(str);
			}
			Assign(id, code) => {
				compileAST(code);
				
				def str = addString(id);
				source.put(loc);
				putb(Opcode.STORE_NAME.tag);
				putb(str);
				source.put(loc);
				putb(Opcode.LOAD_NAME.tag);
				putb(str);
			}
			Raise(exn) => {
				compileAST(exn);
				source.put(loc);
				putb(Opcode.RAISE.tag);
				putb(0); // no operand
			}
			Catch(try, id, catch) => {
				source.put(loc);
				putb(Opcode.TRY.tag);
				def hole1 = pos();
				skipN(1);
				compileAST(try);
				source.put(loc);
				putb(Opcode.CATCH.tag);
				putb(0); // no operand
				source.put(loc);
				putb(Opcode.JUMP.tag);
				def hole2 = pos();
				skipN(1);
				writeb(hole1, pos() - hole1 - 1);

				source.put(loc);
				putb(Opcode.STORE_NAME.tag);
				putb(addString(id));

				compileAST(catch);
				writeb(hole2, pos() - hole2 - 1);
			}
			Begin(exprs) => {
				for (i < exprs.length - 1) {
					compileAST(exprs[i]);
					source.put(loc);
					putb(Opcode.PULL.tag);
					putb(1); // ignore this value
				}
				compileAST(exprs[exprs.length - 1]);
			}
			While(cond, body) => {
				def head = pos();
				compileAST(cond);
				source.put(loc);
				putb(Opcode.JUMP_IF_FALSE_PEEK.tag);
				def hole = pos();
				skipN(1); // hole to put jump offset
				compileAST(body);
				source.put(loc);
				putb(Opcode.PULL.tag);
				putb(100);
				source.put(loc);
				putb(Opcode.JUMP.tag);
				skipN(1);
				writeb(pos() - 1, head - pos());
				writeb(hole, pos() - hole - 1);
			}
			GetField(id, obj) => {
				compileAST(obj);
				source.put(loc);
				putb(Opcode.GET_FIELD.tag);
				putb(addString(id));
			}
			SetField(id, obj, val) => {
				compileAST(obj);
				compileAST(val);
				source.put(loc);
				putb(Opcode.SET_FIELD.tag);
				putb(addString(id));
			}
			Class(id, superclass, init, fields, methods) => {
				// there are subcodes which are getting compiled here
				// TODO should this compilation process just be part of code creation?
				match (superclass) {
					Nothing => {
						def idx = constants.length;
						source.put(loc);
						putb(Opcode.LOAD_LIT.tag);
						putb(addConstant(ClassObject.new(Objects.classBase)));
					}
					Just(expr) => compileAST(expr);
				}
				var init2: Code;
				match (init) {
					Nothing => ;
					Just(code) => {
						code.compile();
						init2 = code;
					}
				}
				//                                   XXX
				def idx = addClasshole(Class.new(id, null, init2, fields, Arrays.map(methods, funcToFunc)));
				source.put(loc);
				putb(Opcode.CREATE_CLASS.tag);
				putb(idx);
				def str = addString(id);
				source.put(loc);
				putb(Opcode.STORE_NAME.tag);
				putb(str);
				source.put(loc);
				putb(Opcode.LOAD_NAME.tag);
				putb(str);
			}
			New(obj) => {
				compileAST(obj);
				source.put(loc);
				putb(Opcode.NEW.tag);
				putb(0); // no operand
			}

//			_ => System.error("compile error", "unsupported AST node");
		}
		
	}

	// wrappers around datawriter
	def putb(b: int) { bytecode.putb(b); }
	def skipN(s: int) { bytecode.skipN(s); }
	def zeroN(s: int) { bytecode.zeroN(s); }
	def pos() -> int { return bytecode.pos; }
	def writeb(addr: int, data: int) { bytecode.data[addr] = byte.view(data); }

	def addString(str: string) -> int {
		def idx = strings.length;
		strings.put(str);
		return idx;
	}

	def addClasshole(clazz: Class) -> int {
		def idx = classholes.length;
		classholes.put(clazz);
		return idx;
	}

	def addConstant(const: Object) -> int {
		def idx = constants.length;
		constants.put(const);
		return idx;
	}

	/* print this bytecode */
	def inspect() {
		def bc = bytecode.copy();
		var pc = 0;
		while (pc < bc.length) {
			def bc      = Ref<Instruction>.at(bc, pc);
			def opcode  = bc.opcode;
			def operand = bc.operand;
			def loc = source[pc >> 1];

			System.puts(Strings.format3("+%d: %s %d", pc, opcode.name, operand));
			System.puts(Strings.format2(" (%d:%d)\n", loc.beginLine, loc.beginColumn));
			
			pc += 2;
			// dispatch on bytecode
			match (opcode) {
				_ => ;
			}
		}

	}
}

layout Instruction {
	+0  opcode:  Opcode;
	+1  operand: byte; // you may treat this as unsigned or signed depending on opcode
	=2;
}

component Bytecodes {
	def inspect(code: Array<byte>) {
		var pc = 0;
		while (pc < code.length) {
			def bytecode = Ref<Instruction>.at(code, pc);
			def opcode   = bytecode.opcode;
			def operand  = bytecode.operand;

			System.puts(Strings.format3("+%d: %s %d", pc, opcode.name, operand));
			
			pc += 2;
			// dispatch on bytecode
			match (opcode) {
				_ => ;
			}
		}
	}
}


// def main(args: Array<string>) -> int {
// 	def code: Array<byte> = [0, 0, 0, 1, 0, 2, 1, byte.view(-3)];
// 
// 	inspect(code);
// 
// 	return 0;
// }
def funcToFunc(f: ASTData.Func) -> Func {
	return Func.new(f.id, f.params, f.body);
}

// TODO
// name, superclass, and init could be represented internally as
// Cicero objects, but fields and methods cannot, so all/none/some?

/* Cicero classes. */
class ClassObject(
		name: string, 
		superclass: ClassObject,  // XXX: base class has superclass == null
		/* CodeObject to evaluate after all fields are initialized */
		init: CodeObject,
		/* List of fields by name and initializer */
		classFields: Array<(string, CodeObject)>, 
		/* Note that the class methods are functions. The function
		 * contains the name of the class. When an object is created,
		 * the object is bound to this function to create the (bound)
		 * method object.
		 */
		classMethods: Array<FuncObject>) extends Object {
		// FIXME perhaps we don't specify methods as func *objects* but funcholes

	/* The name is immutable, so we get the hash value for it once so we
	 * don't have to call Strings.hash every time we want to hash the
	 * ClassObject.
	 */
	def nameH = Strings.hash(name);
	def hash() -> int {
		return nameH;
	}

	def display() -> string { return Strings.format1("<class '%s'>", name); }
	def isTrue()  -> bool   { return true; }

	/* METHODS */

	def c_superclass() -> Result {
		if (superclass == null) {
			return Result.Exn(ExnObjects.fromVirgil(Strings.format1("%s.superclass", name), "class has no superclass"));
		} else {
			return Result.OK(superclass);
		}
	}
	def c_name() -> Result {
		return Result.OK(StrObjects.getStrObject(name));
	}
	def c_display() -> Result {
		return Result.OK(StrObjects.fromString(display()));
	}
}

/* Arrays for the list of types of parameters to the method. These are
 * lazily-defined (because the class doesn't exist yet) and initialized 
 * after all the class objects are defined.
 */
def b:  Array<ClassObject> = Array.new(1);
def o:  Array<ClassObject> = Array.new(1);
def s:  Array<ClassObject> = Array.new(1);
def i:  Array<ClassObject> = Array.new(1);
def io: Array<ClassObject> = Array.new(2);
def so: Array<ClassObject> = Array.new(2);

/* The built-in classes.
 *
 * The order of these is alphabetical, which works out find. Any ordering of
 * the built-in classes must satisfy a topological ordering of the tree 
 * structure of the class hierarchy.
 *
 * Two notes:
 *
 *
 * Besides base, no other object of a built-in class can be instantiated using
 * `new`. This is accomplished by an `init` CodeObject that will always raise 
 * an exception.
 *
 * No built-in classes use fields (besides methods). Note that the underlying
 * Virgil object may have fields corresponding to the underlying value of the
 * object (for example, a BoolObject contains a bool field with the truth
 * value). Some classes have fields which you may think could be exposed as a
 * Cicero field (for example, a FuncObject has a name which you might want to
 * be a StrObject). They are not (see the discussion in `docs/ideas.md`).
 * Instead, FuncObject has an accessor method called `name` which returns the
 * name (Virgil str) as a Cicero StrObject. Because you can overwrite the field
 * of any object you can set this field, but you're not setting the internal
 * name; you're overwriting the method used to access the internal name of the
 * function. Because of this, you can't simply mutate the internal state of an
 * object of a built-in class.
 */
component ClassObjects {
	def classBase = ClassObject.new("base", null,
			null,
			[],
			// cannot initialize Func (or underlying Code) because the class doesn't exist yet
			// we can refactor to reduce the need for the FuncObject, but not the CodeObject

			[FuncObjects.fromVirgilMethod0("class",        Object.o_getclass), // 0
			 FuncObjects.fromVirgilMethod0("fields",       Object.o_fields),   // 1
			 FuncObjects.fromVirgilMethod0("display",      Object.o_display),  // 2
			 FuncObjects.fromVirgilMethod1("get-field", s, Object.o_getfield), // 3
			 FuncObjects.fromVirgilMethod1("=",  o,        Object.o_eq)        // 4
			]);
	def classBool =	ClassObject.new("bool", classBase, 
			CodeObjects.virgilFail, 
			[], 
			[FuncObjects.fromVirgilMethod0("display", BoolObject.b_display),
			 FuncObjects.fromVirgilMethod0("not",     BoolObject.b_not),
			 FuncObjects.fromVirgilMethod1("and", b,  BoolObject.b_and),
			 FuncObjects.fromVirgilMethod1("or",  b,  BoolObject.b_or)
			]);
	def classChar  = ClassObject.new("char",  classBase, CodeObjects.virgilFail, [],
			[FuncObjects.fromVirgilMethod0("display", CharObject.ch_display),
			 FuncObjects.fromVirgilMethod0("ascii", CharObject.ch_ascii_value),
			 FuncObjects.fromVirgilMethod1("+", o,    CharObject.ch_concat)
			]);
	def classClass = ClassObject.new("class", classBase,
			CodeObjects.virgilFail, 
 			[], 
 			[FuncObjects.fromVirgilMethod0("display",    ClassObject.c_display),
 			 FuncObjects.fromVirgilMethod0("superclass", ClassObject.c_superclass),
 			 FuncObjects.fromVirgilMethod0("name",       ClassObject.c_name)
 			]);
	def classCode  = ClassObject.new("code",  classBase, CodeObjects.virgilFail, [],
			[FuncObjects.fromVirgilMethod0("display",     CodeObject.co_display),
			 FuncObjects.fromVirgilMethod0("disassemble", CodeObject.co_disassemble)]);
	def classDouble  = ClassObject.new("double",  classBase, CodeObjects.virgilFail, [],
			[FuncObjects.fromVirgilMethod0("display",     DoubleObject.do_display)
			]);
	def classExn   = ClassObject.new("exn",   classBase, CodeObjects.virgilFail, [], 
			[FuncObjects.fromVirgilMethod0("display",    ExnObject.e_display),
			 FuncObjects.fromVirgilMethod0("stacktrace", ExnObject.e_stacktrace)]);
	def classFrame = ClassObject.new("frame", classBase, CodeObjects.virgilFail, [],
			[FuncObjects.fromVirgilMethod0("display", FrameObject.fr_display)]);
	def classFunc = ClassObject.new("func", classBase, 
			CodeObjects.virgilFail,
			[],
			[FuncObjects.fromVirgilMethod0("display", FuncObject.f_display),
			 FuncObjects.fromVirgilMethod0("name",    FuncObject.f_name),
			 FuncObjects.fromVirgilMethod0("params",  FuncObject.f_params),
			 FuncObjects.fromVirgilMethod0("code",    FuncObject.f_code)
			]);
	def classInt = ClassObject.new("int", classBase, 
			CodeObjects.virgilFail, 
			[], 
			[FuncObjects.fromVirgilMethod0("display", IntObject.i_display),
			/* built-in classes can have methods defined in Cicero! */
			 FuncObjects.fromCiceroMethod ("succ",    CiceroTexts.i_succ),
			 FuncObjects.fromCiceroMethod ("pred",    CiceroTexts.i_pred),
			 FuncObjects.fromVirgilMethod0("neg",     IntObject.i_neg),
			 FuncObjects.fromVirgilMethod1("+",  i,   IntObject.i_add),
			 FuncObjects.fromVirgilMethod1("-",  i,   IntObject.i_sub),
			 FuncObjects.fromVirgilMethod1("*",  i,   IntObject.i_mul),
			 FuncObjects.fromVirgilMethod1("/",  i,   IntObject.i_div),
			 FuncObjects.fromVirgilMethod1("%",  i,   IntObject.i_mod),
			 FuncObjects.fromVirgilMethod1("=",  o,   IntObject.i_eq),
			 FuncObjects.fromVirgilMethod1("<",  i,   IntObject.i_lt),
			 FuncObjects.fromVirgilMethod1("<=", i,   IntObject.i_leq),
			 FuncObjects.fromVirgilMethod1(">",  i,   IntObject.i_gt),
			 FuncObjects.fromVirgilMethod1(">=", i,   IntObject.i_geq),
			 FuncObjects.fromVirgilMethod1("&",  i,   IntObject.i_and),
			 FuncObjects.fromVirgilMethod1("|",  i,   IntObject.i_ora),
			 FuncObjects.fromVirgilMethod1("^",  i,   IntObject.i_eor),
			 FuncObjects.fromVirgilMethod1(">>", i,   IntObject.i_shr),
			 FuncObjects.fromVirgilMethod1("<<", i,   IntObject.i_shl)
			]);
	def classList = ClassObject.new("list", classBase, 
			CodeObjects.virgilFail, 
			[], 
			[FuncObjects.fromVirgilMethod0("display", ListObject.l_display),
			 FuncObjects.fromCiceroMethod ("=",       CiceroTexts.l_eq),
			 FuncObjects.fromVirgilMethod1("get", i,  ListObject.l_get),
			 FuncObjects.fromVirgilMethod2("set", io, ListObject.l_set),
			 FuncObjects.fromVirgilMethod1("put", o,  ListObject.l_put),
			 FuncObjects.fromVirgilMethod0("length",  ListObject.l_length),
			 FuncObjects.fromVirgilMethod0("reverse", ListObject.l_reverse)
			]);
	def classMap = ClassObject.new("map", classBase, CodeObjects.virgilFail, [],
			[FuncObjects.fromVirgilMethod2("set",    so, MapObject.p_set),
			 FuncObjects.fromVirgilMethod1("get",    s,  MapObject.p_get),
			 FuncObjects.fromVirgilMethod1("delete", s,  MapObject.p_delete),
			 FuncObjects.fromVirgilMethod0("keys",       MapObject.p_keys),
			 FuncObjects.fromVirgilMethod0("values",     MapObject.p_values),
			 FuncObjects.fromVirgilMethod0("display", MapObject.p_display)]);
	def classMethod = ClassObject.new("method", classFunc, 
			CodeObjects.virgilFail,
			[], 
			[FuncObjects.fromVirgilMethod0("display", MethodObject.m_display),
			 FuncObjects.fromVirgilMethod0("object",  MethodObject.m_object)]);
	def classPoopCrap = ClassObject.new("poopcrap", classBase, CodeObjects.virgilFail, [], 
			[FuncObjects.fromVirgilMethod0("display", PoopCrapObject.u_display)]);
	def classStr = ClassObject.new("str", classBase, CodeObjects.virgilFail, [],
			[FuncObjects.fromVirgilMethod0("display",    StrObject.s_display),
			 FuncObjects.fromVirgilMethod0("length",     StrObject.s_length),
			 FuncObjects.fromVirgilMethod0("chars",      StrObject.s_chars),
			 FuncObjects.fromVirgilMethod1("+", o,       StrObject.s_concat),
			 FuncObjects.fromVirgilMethod1("char-at", i, StrObject.s_charAt)]);


	/* Returns the function which creates class objects when given the
	 * superclass object.
	 */
	def classhole(name: string, init: CodeObject, classFields: Array<(string, CodeObject)>, classMethods: Array<FuncObject>) -> (ClassObject -> ClassObject) {
		return ClassObject.new(name, _, init, classFields, classMethods);
	}
}

def stuff1 = o[0]  = ClassObjects.classBase;
def stuff2 = b[0]  = ClassObjects.classBool;
def stuff3 = i[0]  = ClassObjects.classInt;
def stuff4 = io[0] = ClassObjects.classInt;
def stuff5 = io[1] = ClassObjects.classBase;
def stuff6 = so[0] = ClassObjects.classStr;
def stuff7 = so[1] = ClassObjects.classBase;
def stuff8 = s[0] = ClassObjects.classStr;

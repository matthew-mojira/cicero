/* FrameObject refers to the `frame` class in Cicero, but in Virgil this is
 * an abstract class extended by Virgil/AST/Bytecode frames internally.
 */
class FrameObject extends Object {
	def code: CodeObject;
	def args: Array<(string, Object)>;
	def display() -> string { return Strings.format1("<frame '%s'>", name); }
	def isTrue()  -> bool   { return true; }
	def var name: string;

	/* private = don't create this class explicitly */
	private new(prev: FrameObject, code, args) {
		// if current frame is the top level frame, the hash-map and array for globals is empty
	}

	// status flags as the result of evaluation
	var result: Result;

	// retrieving results, but check first!
	def getValue() -> Object {
		if (Result.OK.?(result))
			return Result.OK.!(result).value;
		else
			return Result.Return.!(result).value;
	}

	def getExn() -> ExnObject {
		return Result.Exn.!(result).exn;
	}

	/* Current location of the evaluation. This doesn't need to be exact, but it
	 * should be correct whenever an exception is raised so that the stacktrace
	 * is accurate.
	 */
	var loc: FileRange;

	/* A function to evaluate this frame. It must set {result} based on 
	 * what happens. */
	def eval();
	/* This is the function used to evaluate top-level code. A frame cannot
	 * normally evaluate (in `eval`) multiple expressions (unless the block
	 * construct is used). But in the top level (especially the repl),
	 * independent expressions are fed in one-by-one.
	 */	
	def evalCode(code: CodeObject) -> Object;

	def newFrame(name: string, code: CodeObject, args: Array<(string, Object)>, nonlocals: Array<(string, Object)>) -> FrameObject {
		return FrameObjects.newFrame(this, name, code, args, nonlocals);
	}

	// throw sets the exception value in the frame right away
	def throw(msg: string) {
		result = Result.Exn(ExnObjects.fromStrObject(StrObjects.fromString(msg), name, loc));
	}
	def rethrow(exn: ExnObject) {
		exn.stacktrace.put((name, loc));
		result = Result.Exn(exn);
	}

	def isExn() -> bool {
		return Result.Exn.?(result);
	}

	def isReturning() -> bool {
		return Result.Return.?(result);
	}

	def isAborting() -> bool {
		return isExn() || isReturning();
	}

	def resetExn() {
		result = Result.Uncomputed;
	}

	def assertType(exp: ClassObject, val: Object) {
		if (!val.instanceOf(exp))
			throw(Exceptions.typeMismatch(exp, val));
	}

	def assertArity<T>(argc: int, vals: Range<T>) {
		if (vals.length != argc)
			throw(Exceptions.arityMismatch(argc, vals.length));
	}

	/* METHODS */

	def fr_display() -> Result {
		return Result.OK(StrObjects.fromString(display()));
	}
}

component FrameObjects {
	/* This is the only place to create new frames. It determines what kind
	 * of frame to create to evaluate the code based on the type of the
	 * Code (AST, bytecode, etc.)
	 */
	def newFrame(prev: FrameObject, name: string, code: CodeObject, args: Array<(string, Object)>, nonlocals: Array<(string, Object)>) -> FrameObject {
		if (CiceroOptions.traceFunctionCalls) {
			System.puts(name);
			System.puts("(");
			for (i < args.length) {
				def arg = args[i];
				System.puts(arg.0);
				System.puts(" = ");
				System.puts(arg.1.display());
				if (i < args.length - 1)
					System.puts(", ");
			}
			System.puts(")\n");
		}

		var frame: FrameObject;
		match (code.code) {
			Virgil   => frame = VirgilFrame.new(prev, code, args);
			AST      => frame = ASTFrame.new(prev, code, args, nonlocals);
			Bytecode => frame = BytecodeFrame.new(prev, code, args, nonlocals);
		}
		frame.name = name;
		return frame;
	}
	/* Frame used to evaluate top-level code. */
	def newTopFrame() -> FrameObject {
		var frame: FrameObject;
		if (CLOptions.TIER.get() > 0) {
			frame = BytecodeFrame.new(null, null, null, null);
		} else {
			frame = ASTFrame.new(null, null, null, null);
		}
		for (builtin in Builtin.set.all) {
			Globals.storeGlobal(builtin.id, builtin.val);
		}
		frame.name = "repl";
		return frame;
	}
}

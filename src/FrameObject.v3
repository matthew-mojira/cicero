class FrameObject(frame: Frame) extends Object {
	new() super(FrameObjects.classFrame) {}
}
class Frame(prev: Frame, 
		locals: HashMap<string, Object>, //  \
		args: Array<Object>,             //  / XXX combine these together
		code: Code) {
	var globals: HashMap<string, Object>;

	// status flags as the result of evaluation
	var exn: bool = false;  // whether evaluation threw uncaught exception
	var val: Object;        // result of evaluation or exception value

	/* Initializing the frame also evaluates it (except for top-level frames). */
	new() {
		if (prev == null) {
			// this is the top-level frame, where locals = globals
			globals = locals;
		} else {
			globals = prev.globals;

			// evaluate the frame
			eval(code);
		}
	}

	/* You should inspect the frame to determine if the value is meaningful. If
	 * there is no exception in the frame, then you can accept the value.
	 * Otherwise, ignore the return value. It is usually, but not guaranteed, to
	 * be null.
	 */
	def eval(code: Code) -> Object {
		// dispatch on the code tier
		def evalFuncs = [virgilEval, tier0Eval];
		def v = evalFuncs[code.tier + 1](code);
		if (!exn)
			val = v;
		return v;
	}

	private def virgilEval(code: Code) -> Object {
		def ret = code.virgil(args);
		val = ret.0;
		exn = ret.1;
		return val;
	}

	private def tier0Eval(code: Code) -> Object {
		var value: Object;

		match (code.ast) {
			Lit(val) => value = val;
			Id(id) => {
				if (locals.has(id)) {
					value = locals[id];
				} else if (globals.has(id)) {
					value = globals[id];
				} else {
					throw(StrObjects.getStrObject(Strings.format1("unbound identifier: %s", id)));
				}
			}
			Apply(tgt, args) => {
				def tgtVal = eval(tgt);
				if (exn) return value;
				assertType(FuncObjects.classFunc, tgtVal);
				if (exn) return value;

				// function code
				def fv = FuncObject.!(tgtVal);
				def params = fv.func.params, code = fv.func.code;

				// evaluate argument values
				var argv: Array<Object> = Array.new(args.length);
				for (i < argv.length) {
					def arg = eval(args[i]);
					if (exn) return value;
					argv[i] = arg;
				}

				// arity check
				assertArity(params.length, argv);
				if (exn) return value;

				// create locals
				def newLocals: HashMap<string, Object> = Strings.newMap();
				for (i < argv.length) {
					newLocals[params[i]] = argv[i];
				}

				// if this is a method, insert extra locals or arguments
				if (fv.instanceOf(MethodObjects.classMethod)) {
					newLocals["self"] = MethodObject.!(fv).obj;
					argv = Arrays.prepend(MethodObject.!(fv).obj, argv);					
				}

				// create new frame (and evaluate)
				def frame = Frame.new(this, newLocals, argv, code);
				if (frame.exn) {
					// rethrow
					throw(frame.val);
					return value;
				}
				value = frame.val;
			}
			If(exprC, exprT, exprF) => {
				def cond = eval(exprC);
				if (exn) return value;

				if (cond.isTrue()) {
					def v = eval(exprT);
					if (exn) return value;
					value = v;
				} else {
					def v = eval(exprF);
					if (exn) return value;
					value = v;
				}
			}
			Raise(expr) => {
				def v = eval(expr);
				if (exn) return value;

				throw(v);
			}
			Catch(try, catch) => {
				def v = eval(try);
				if (!exn) {
					value = v;
				} else {
					// catch exception
					exn = false;
					def e = eval(catch);
					if (exn) return value;
					value = e;
				}
			}
			Func(id, params, code) => {
				def c = CodeObject.new(code);
				def f = FuncObjects.newFO(Func.new(id, params, c.code));
				locals[id] = f;
				value = f;
			}
			Class(id, superclass, init, fields, methods) => {
				var i: Code = if(init.isJust(), init.fromJust(), null);
				var s: Class = Objects.classBase;
				// TODO match on {superclass} and evaluate expression
				def f: Array<(string, Code)> = fields;
				def m: Array<Func> = Arrays.map(methods, funcToFunc);
				def c = ClassObject.new(Class.new(id, s, i, f, m));
				locals[id] = c;
				value = c;
			}
			Assign(id, expr) => {
				def v = eval(expr);
				if (exn) return value;
				locals[id] = v;
				value = v;
			}
			Cond(cases) => {
				var sat = false;
				for (c in cases) {
					// case
					def cond = c.0, expr = c.1;

					// check cond
					def t = eval(cond);
					if (exn) return value;
					if (t.isTrue()) {
						def v = eval(expr);
						if (exn) return value;
						value = v;

						sat = true;
						break;
					}
				}

				if (!sat) {
					throw(StrObjects.getStrObject("cond error: no true branch"));
				}
			}
			While(cond, body) => {
				while (true) {
					def v = eval(cond);
					if (exn) return value;

					if (!v.isTrue()) {
						value = v;
						break;
					}

					eval(body);
					if (exn) return value;
				}
			}
			Begin(exprs) => {
				var last: Object;
				for (expr in exprs) {
					def v = eval(expr);
					if (exn) return value;

					last = v;
				}
				value = last;
			}
			GetField(field, expr) => {
				var obj = eval(expr);
				if (exn) return value;

				match (obj.getField(field)) {
					Just(v) => value = v;
				} else {
					throw(StrObjects.getStrObject(Strings.format1("name error: unknown field %s", field)));
					return value;
				}
			}
			SetField(field, expr, newCode) => {
				var obj = eval(expr);
				if (exn) return value;

				var val = eval(newCode);
				if (exn) return value;

				value = obj.fields[field];
				if (obj.fields.has(field)) {
					obj.fields[field] = val;
					value = val;
				} else {
					throw(StrObjects.getStrObject(Strings.format1("name error: unknown field %s", field)));
					return value;
				}
			}
			New(expr) => {
				var cls = eval(expr);
				if (exn) return value;

				assertType(ClassObjects.classClass, cls);
				if (exn) return value;

				def clsObj = ClassObject.!(cls);
				def obj = Object.new(clsObj.clazz);

				// initialize fields in superclass order
				def classes: Vector<Class> = Vector.new();
				for (cls = clsObj.clazz; cls != null; cls = cls.superclass)
					classes.put(cls);
				
				for (cls in classes.extract()) {
					// initialize fields
					for (field in cls.classFields) {
						def name = field.0, expr = field.1;
						// FIXME this is incorrect if a child class overrides a field with a method
						if (obj.fields.has(name)) continue;

						// XXX hackfix
						if (expr == null) continue;
						var v = eval(expr);
						if (exn) return value;
						obj.fields[name] = v;
					}
					// initialize methods?
// 					for (func in cls.classMethods) {
// 						if (obj.fields.has(func.id)) continue;
// 						def m = MethodObject.new(obj, func.id, func.params, func.code);
// 						obj.fields[func.id] = m;
// 					}
					// call initializer
					if (cls.init != null) {
						System.puts(Strings.format1("> %s\n", cls.name));
						def code = cls.init;

						def newLocals: HashMap<string, Object> = Strings.newMap();
						newLocals["self"] = obj;
						def argv: Array<Object> = [obj];

						// create new frames here!
						def frame = Frame.new(this, newLocals, argv, code);
						if (frame.exn) {
							// rethrow
							throw(frame.val);
							return value;
						}
						value = frame.val;
					}
				}

				return obj;
			}
		}

		return value;
	}
	
	def display() -> string { return "<frame>"; }
	def isTrue()  -> bool   { return true; }

	/* throwing exceptions */

	// throw sets the exception value in the frame right away
	private def throw(value: Object) {
		exn = true;
		val = value;
	}

	private def assertType(exp: Class, val: Object) {
		if (!val.instanceOf(exp))
			throw(StrObjects.getStrObject(Strings.format2("type mismatch: expected %s, got %s", exp.display(), val.cls.display())));
	}

	private def assertArity(argc: int, vals: Range<Object>) {
		if (vals.length != argc)
			throw(StrObjects.getStrObject(Strings.format2("arity mismatch: expected %d, got %d", argc, vals.length)));
	}

}

component FrameObjects {
	def classFrame = Class.new("frame", Objects.classBase, Codes.virgilFail, [], []);
}

def funcToFunc(f: AST.Func) -> Func {
	// FIXME null
	return Func.new(f.id, f.params, Codes.fromAST(f, null));	
}



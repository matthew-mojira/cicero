class FrameValue(prev: FrameValue, locals: HashMap<string, Value>, code: Expr) extends Value {
	var globals: HashMap<string, Value>;

	// you could replace this with a higher-tier evaluation function
	var evalFunc: Expr -> Value;

	// status flags as the result of evaluation
	var exn: bool = false;  // whether evaluation threw uncaught exception
	var val: Value;         // result of evaluation or exception value

	/* Initializing the frame also evaluates it (except for top-level frames). */
	new() {
		evalFunc = evalExpr;
		if (prev == null) {
			// this is the top-level frame, where locals = globals
			globals = locals;
		} else {
			globals = prev.globals;

			// evaluate the frame
			def v = evalFunc(code);
			if (!exn)
				val = v;
		}
	}

	// special function for top-level frame to let it evaluate multiple expressions 
	def eval(expr: Expr) {
		if (prev != null) System.error("eval error", "attempted to evaluate expression not in top level");

		def v = evalFunc(expr);
		if (!exn)
			val = v;
		// XXX no return value, inspect the frame instead
	}

	private def evalExpr(expr: Expr) -> Value {
		var value: Value;

		match (expr.data) {
			Lit(val) => value = val;
			Id(id) => {
				if (locals.has(id)) {
					value = locals[id];
				} else if (globals.has(id)) {
					value = globals[id];
				} else {
					throw(StrValues.getStrValue(Strings.format1("unbound identifier: %s", id)));
				}
			}
			Apply(tgt, args) => {
				def tgtVal = evalExpr(tgt);
				// remember: these are recursive calls within `evalExpr` but
				// have nothing to do with evaluation frames in the language
				if (exn) return value;
				assertType(Type.Func, tgtVal);
				if (exn) return value;

				def argv: Array<Value> = Array.new(args.length);
				for (i < argv.length) {
					def arg = evalExpr(args[i]);
					if (exn) return value;
					argv[i] = arg;
				}

				// function call
				if (FuncValue.?(tgtVal)) {
					def fv = FuncValue.!(tgtVal);
					def id = fv.id, params = fv.params, code = fv.code;

					// arity check
					assertArity(params.length, argv);
					if (exn) return value;

					def newLocals: HashMap<string, Value> = Strings.newMap();
					for (i < argv.length) {
						newLocals[params[i]] = argv[i];
					};
					
					// create new frames here!
					def frame = FrameValue.new(this, newLocals, code);
					if (frame.exn) {
						// rethrow
						throw(frame.val);
						return value;
					}
					value = frame.val;
					
				} else if (BuiltinValue.?(tgtVal)) {
					def bv = BuiltinValue.!(tgtVal);
					def func = bv.func;

					// function call
					def ret = func(argv); // XXX built-ins check their own arity
					def val = ret.0, fnExn = ret.1;

					if (fnExn) {
						throw(val);
						return value;
					} else {
						value = val;
					}	
				} else {
					System.error("eval error", "unreachable case");
				}
			}
			If(exprC, exprT, exprF) => {
				def cond = evalExpr(exprC);
				if (exn) return value;

				if (cond.isTrue()) {
					def v = evalExpr(exprT);
					if (exn) return value;
					value = v;
				} else {
					def v = evalExpr(exprF);
					if (exn) return value;
					value = v;
				}
			}
			Raise(expr) => {
				def v = evalExpr(expr);
				if (exn) return value;

				throw(v);
			}
			Catch(try, catch) => {
				def v = evalExpr(try);
				if (!exn) {
					value = v;
				} else {
					// catch exception
					exn = false;
					def e = evalExpr(catch);
					if (exn) return value;
					value = e;
				}
			}
			Func(id, params, body) => {
				def f = FuncValue.new(id, params, body);
				locals[id] = f;
				value = f;
			}
			Assign(id, expr) => {
				def v = evalExpr(expr);
				if (exn) return value;
				locals[id] = v;
				value = v;
			}
			Cond(cases) => {
				var sat = false;
				for (c in cases) {
					// case
					def cond = c.0, expr = c.1;

					// check cond
					def t = evalExpr(cond);
					if (exn) return value;
					if (t.isTrue()) {
						def v = evalExpr(expr);
						if (exn) return value;
						value = v;

						sat = true;
						break;
					}
				}

				if (!sat) {
					throw(StrValues.getStrValue("cond error: no true branch"));
				}
			}
			While(cond, body) => {
				while (true) {
					def v = evalExpr(cond);
					if (exn) return value;

					if (!v.isTrue()) {
						value = v;
						break;
					}

					evalExpr(body);
					if (exn) return value;
				}
			}
			Begin(exprs) => {
				var last: Value;
				for (expr in exprs) {
					def v = evalExpr(expr);
					if (exn) return value;

					last = v;
				}
				value = last;
			}
		}

		return value;
	}
	
	def typeof()  -> Type   { return Type.Frame; }
	def display() -> string { return "<frame>"; }
	def isTrue()  -> bool   { return true; }

	/* throwing exceptions */

	// throw sets the exception value in the frame right away
	private def throw(value: Value) {
		exn = true;
		val = value;
	}

	private def assertType(exp: Type, val: Value) {
		if (exp != val.typeof())
			throw(StrValues.getStrValue(Strings.format2("type mismatch: expected %s, got %s", exp.display(), val.typeof().display())));
	}

	private def assertArity(argc: int, vals: Range<Value>) {
		if (vals.length != argc)
			throw(StrValues.getStrValue(Strings.format2("arity mismatch: expected %d, got %d", argc, vals.length)));
	}

}

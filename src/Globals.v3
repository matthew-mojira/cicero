component Globals {
   // Globals cannot be deleted in cicero
   def values: Vector<Object> = Vector.new(); // holds all the values and grows as new globals are added
   def indices: HashMap<string, int> = Strings.newMap(); // holds a mapping of global variable names and its index in values

   // Returns the index of the stored global object in the `Globals.values` vector
   def storeGlobal(id:string, v: Object) -> int{
      var index: int;
      if (indices.has(id)){
         index = indices[id];
         values[index] = v;
      } else {
         index = values.length;
         indices[id] = index;
         values.put(v);
      }
      return index;
   }

   // if valid lookup -> returns the `Object` 
   //    as well as its index in the `Globals.values` vector for bytecode rewriting purposes
   def loadGlobal(id: string) -> Maybe<(Object, int)>{
      if (indices.has(id)){
         def index = indices[id];
         def v = values[index];
         return Maybe.Just((v, index));
      } else {
         return Maybe.Nothing;
      }
   }

   /* 
      Optimized functions that bypass the map lookup for globals
      Used by `LOAD_GLOBAL_BY_INDEX` and `STORE_GLOBAL_BY_INDEX` opcodes
   */

   def storeGlobalByIndex(index: int, v: Object){
      values[index] = v;
   }
   def loadGlobalByIndex(index: int) -> Object{
      return values[index];
   }
}

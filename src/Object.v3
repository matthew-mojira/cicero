/* cls represents a linked list of classes up to the base class
 * 
 * fields is an array of Objects which hold each field, starting from the
 * base class down to the lowest class (cls).
 * 
 * For each class, we separate fields and methods (since they're separated
 * like that in the field), but here they are stored in the same array. The
 * bona-fide fields are stored first, then the methods, for each class from
 * base down to the lowest class.
 *
 * As an example:
 *   (class A
 *      (field a1)
 *      (method (a2 ...) ...)
 *      (method (a3 ...) ...)
 *      (field a4))
 *   (class B
 *      (extends A)
 *      (field b1)
 *      (field b2)
 *      (method (a2 ...) ..))
 * is stored as
 *   from base:
 *     0: class
 *     1: fields
 *     2: display
 *     3: =
 *     4: !=
 *   from A:
 *     5: a1
 *     6: a4
 *     7: a3 <- method is last
 *     8: a2 <- this method is overwritten from B and still goes here
 *   from B:
 *     8: b1
 *     9: b2
 *
 * Note that methods are lazily allocated (since they're objects, allocating
 * them eagerly means allocating its methods, creating an infinite loop). These
 * are represented by `null` values. These are the only null values allowed
 * in the array, as the language requires that all fields be initialized (they
 * may be initialized with the language-level poopcrap).
 */
class Object {
	private var cls: ClassObject;
	private def fieldMap: HashMap<string, int> = Strings.newMap();
	private var fields:   Array<Object>;

	new() {}

	/* Objects of the built-in classes are eagerly allocated (as needed),
	 * but lazily initialized. This is because of depedencies between 
	 * objects of the built-in classes, where allocation is sufficient to
	 * glue the objects together. This means, however, that we must be
	 * careful not to perform an operation on an uninitialized object.
	 *
	 * The objects that always exist because of the runtime system are:
	 * - the built-in classes
	 * - initializer functions for the built-in classes
	 * - functions which represent possible methods on the built-in
	 *   classes (note: we should probably make this not a function *object*)
	 *
	 * Objects of user-defined classes are eagerly initialized.
	 */
	private def initClass() {
		/* Built-in class? Set the class. */
		/* XXX: you cannot extend a built-in class, so this is fine, but
		 * maybe we want to allow this */
		match (this) {
 	 		x: MethodObject => cls = ClassObjects.classMethod;
 	 		x: BoolObject   => cls = ClassObjects.classBool;
 	 		x: ClassObject  => cls = ClassObjects.classClass;
	 		x: CodeObject   => cls = ClassObjects.classCode;
	 		x: ExnObject    => cls = ClassObjects.classExn;
 	 		x: FrameObject  => cls = ClassObjects.classFrame;
 	 		x: FuncObject   => cls = ClassObjects.classFunc;
 	 		x: IntObject    => cls = ClassObjects.classInt;
 	 		x: ListObject   => cls = ClassObjects.classList;
 	 		x: MapObject    => cls = ClassObjects.classMap;
 	 		x: PoopCrapObject => cls = ClassObjects.classPoopCrap;
 	 		x: StrObject    => cls = ClassObjects.classStr;
		}

		// sanity check
		if (CiceroOptions.sanityChecks)
			if (cls == null) System.error("init error", "class is still null after initialization");

		/* Get the sequence of superclasses up to base */
		def classes = Vector<ClassObject>.new();
 		for (clazz = this.cls; clazz != null; clazz = clazz.superclass) {
			classes.put(clazz);
		}
		/* Figure out how much space needs to be allocated for fields. */
		var i = 0;
		for (clazz in classes.reverse().copy()) {
			for (field in clazz.classFields) {
				if (!fieldMap.has(field.0))
					fieldMap[field.0] = i++;
			}
			// methods are not initialized but a slot is allocated
			for (method in clazz.classMethods) {
				if (!fieldMap.has(method.id))
					fieldMap[method.id] = i++;
			}
		}
		fields = Array.new(i);
		/* Initialization sets classes, allocates space for fields, and
		 * initializes methods. Setting the fields are done later. 
		 * Non-method fields
		 * are eagerly initialized by the runtime for user-defined classes.
		 * No built-in class makes use of non-method fields, but maybe this
		 * could change if we expose *primitive* fields which are also
		 * Cicero objects.
		 */
		
		// Methods are initialized when class is initialized
		if (!CiceroOptions.useLazyMethods) {
			for (clazz in classes.copy()) {
				for (func in clazz.classMethods) {
					def m = MethodObject.new(this, func.id, func.params, func.code);
					fields[fieldMap[func.id]] = m;
				}
			}
		}
	}

	// returns success
	// FALSE = FAILURE
	def setField(name: string, obj: Object) -> bool {
		if (cls == null) initClass();

		if (!fieldMap.has(name)) return false;
		fields[fieldMap[name]] = obj;
		return true;
	}
	def setFieldNull(name: string, obj: Object) -> Object {
		if (cls == null) initClass();

		if (!fieldMap.has(name)) return null;
		fields[fieldMap[name]] = obj;
		return obj;
	}

 	def setFieldByIndex(idx: int, obj: Object) {
 		fields[idx] = obj;
 	}
 
 	def getFieldByIndex(idx: int) -> Object {
		if (CiceroOptions.sanityChecks) {
			if (cls == null)
				System.error("field error", "class not initialized");
			if (idx < 0 || idx >= fields.length) {
				System.error("field error", Strings.format3("field index %d is out of bounds for class %s (length %d)",
							idx, cls.name, fields.length));
			}
			if (fields[idx] == null) {
				System.error("field error", Strings.format2("access of field index %d for class %s is null",
							idx, cls.name));
			}
		}
 		return fields[idx];
 	}

 	def getIndex(field: string) -> int {
		if (fieldMap.has(field)) {
			return fieldMap[field];
		}
		System.error("object error", Strings.format1("getting index of field not present: %s", field));
		return -999;
 	}

	// returns null on failure
	def getField(name: string) -> Object {
		if (cls == null) initClass();

		// not a field of this class
		if (!fieldMap.has(name)) return null;

		def obj = fields[fieldMap[name]];
		if (obj != null) return obj; // allocated
	
		if (CiceroOptions.useLazyMethods) {
			// null if and only if lazy (uninitialized) method
			for (clazz = this.cls; clazz != null; clazz = clazz.superclass) {
				for (func in clazz.classMethods) {
					if (Strings.equal(func.id, name)) {
						def m = MethodObject.new(this, func.id, func.params, func.code);
						fields[fieldMap[name]] = m;
						return m;
					}
				}
			}

			System.error("object error", Strings.format1("uninitialized field not a lazy method: %s", name));
			return null;
		}

		System.error("object error", Strings.format1("could not find field %s", name));
		return null;
	}

	def getClass() -> ClassObject {
		if (cls == null) initClass();
		return cls;
	}

	def instanceOf(otherCls: ClassObject) -> bool {
		if (cls == null) initClass();
 		for (clazz = this.cls; clazz != null; clazz = clazz.superclass) {
			if (clazz == otherCls) {
				return true;
			}
		}
		return false;
	}

	def display() => "<object>";
	def isTrue()  => true;

	def o_getclass() -> Result {
		return Result.OK(cls);
	}
	def o_fields() -> Result {
		def list = ListObject.new();
		def fList = Maps.keyList(fieldMap);
		Lists.apply(fList, putList(list, _));
		return Result.OK(list);
	}
	def o_display() -> Result {
		return Result.OK(StrObjects.getStrObject("<object>"));
	}
	def o_getfield(str: StrObject) -> Result {
		def field = getField(str.str);
		// field is null = not found
		if (field == null) {
			return Result.Exn(ExnObjects.fromVirgil("get-field", Exceptions.missingField(this, str.str)));
		}
		return Result.OK(field);
	}
	def o_eq(other: Object) -> Result {
		return Result.OK(BoolObjects.fromBool(this == other));
	}
	def o_neq(other: Object) -> Result {
		return Result.OK(BoolObjects.fromBool(this != other));
	}
}


component Objects {
	def display = 2;
	def eq = 3;
	
	def newObject(cls: ClassObject) -> Object {
		def obj = Object.new();
		obj.cls = cls;
		obj.initClass();
		return obj;
	}
}

def putList(list: ListObject, str: string) {
	def obj = StrObjects.getStrObject(str);
	list.list.put(obj);
}


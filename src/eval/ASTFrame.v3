class ASTFrame extends FrameObject {
	new(prev: FrameObject, code: CodeObject, args: Array<(string, Object)>, nonlocalArgs: Array<(string, Object)>) super(prev, code, args) {
		if (args != null) {
			for (arg in args) {
				locals[arg.0] = arg.1;
			}
		}
		if (nonlocalArgs != null) {
			for (nonlocal in nonlocalArgs) {
				nonlocals[nonlocal.0] = nonlocal.1;
			}
		}
	}
	
	def var locals: HashMap<string, Object> = Strings.newMap();
	def var nonlocals: HashMap<string, Object> = Strings.newMap();
	
	def eval() {
		def v = evalAST(Code.AST.!(code.code).ast);
		if (!isExn())
			result = Result.OK(v);			
	}

	def evalCode(code: CodeObject) -> Object {
		def v = evalAST(Code.AST.!(code.code).ast);
		if (!isExn())
			result = Result.OK(v);			
		return v;
	}

	private def evalAST(ast: AST) -> Object {
		loc = ast.loc;
		var value: Object;

		match (ast.data) {
			Lit(val) => value = val;
			VarGet(id, mode) => match (mode.data) {
				Local => {
					if (locals.has(id))
						value = locals[id];
					else {
						throw(Exceptions.unboundLocal(id));
						return value;
					}
				}
				Global => {
					match (Globals.loadGlobal(id)){
						Some(v) => value = v;
						None => {
							throw(Exceptions.unboundGlobal(id));
							return value;
						}
					}
				}
				Nonlocal => {
					if (CiceroOptions.sanityChecks) {
						if (nonlocals[id] == null)
							System.error("interp error", Strings.format1("nonlocal read of %s was null", id));
					}
					value = nonlocals[id];
				}
			}
			VarSet(id, mode, expr) => {
				def v = evalAST(expr);
				if (isExn()) return value;
				match (mode.data) {
					Local  => locals[id] = v;
					Global => Globals.storeGlobal(id, v);
					Nonlocal => System.error("interp error", "attempting to set nonlocal variable");
				}
				value = v;
			}
			And(left, right) => {
				def lval = evalAST(left);
				if (isExn()) return value;

				if (lval.isTrue()) {
					def rval = evalAST(right);
					if (isExn()) return value;
					value = rval;
				} else {
					value = lval;
				}
			}
			Or(left, right) => {
				def lval = evalAST(left);
				if (isExn()) return value;

				if (lval.isTrue()) {
					value = lval;
				} else {
					def rval = evalAST(right);
					if (isExn()) return value;
					value = rval;
				}
			}
			Apply(tgt, args) => {
				def tgtVal = evalAST(tgt);
				if (isExn()) return value;
				assertType(ClassObjects.classFunc, tgtVal);
				if (isExn()) return value;

				// function code
				def fv = FuncObject.!(tgtVal);
				def params = fv.params, code = fv.code;

				// evaluate argument values
				var argv: Array<(string, Object)>;

				if (fv.instanceOf(ClassObjects.classMethod)) {
					argv = Array.new(args.length + 1);
					for (i < args.length) {
						def arg = evalAST(args[i]);
						if (isExn()) return value;
						if (i < params.length) // avoid out of bounds but don't raise exception
							argv[i + 1] = (params[i], arg);
					}
					argv[0] = ("self", MethodObject.!(fv).obj);
				} else {
					argv = Array.new(args.length);
					for (i < args.length) {
						def arg = evalAST(args[i]);
						if (isExn()) return value;
						if (i < params.length) // avoid out of bounds but don't raise exception
							argv[i] = (params[i], arg);
					}
				}

				// arity check
				assertArity(params.length, args);
				if (isExn()) return value;

				// create new frame (and evaluate)
				def frame = newFrame(fv.id, code, argv, fv.nonlocals);
				frame.eval();
				if (frame.isExn()) {
					rethrow(frame.getExn());
					return value;
				}
				value = frame.getValue();
			}
			If(exprC, exprT, exprF) => {
				def cond = evalAST(exprC);
				if (isExn()) return value;

				if (cond.isTrue()) {
					def v = evalAST(exprT);
					if (isExn()) return value;
					value = v;
				} else {
					def v = evalAST(exprF);
					if (isExn()) return value;
					value = v;
				}
			}
			Raise(expr) => {
				def v = evalAST(expr);
				if (isExn()) return value;

				// Raise a string for the message
				assertType(ClassObjects.classStr, v);
				if (isExn()) return value;

				throw(StrObject.!(v).str);
			}
			Catch(try, exnvar, b, catch) => {
				def v = evalAST(try);
				if (!isExn()) {
					value = v;
				} else {
					// catch exception
					def val = getExn();
					resetExn();
					match (b.data) { // bind exception to variable
						Local =>  locals[exnvar]  = val;
						Global => Globals.storeGlobal(exnvar, val);
						Nonlocal => System.error("interp error", "trying to bind to a nonlocal variable");
					}
					def e = evalAST(catch);
					if (isExn()) return value;
					value = e;
				}
			}
			Func(id, params, code, fnonlocals) => {
				def vals: Array<(string, Object)> = Array.new(fnonlocals.length);
				// create the function now, and insert its values as we iterate through fnonlocals
				def fn = FuncObjects.newFO(id, params, vals, code);
				// now work: capturing free variables
				for (i < fnonlocals.length) {
					def lid = fnonlocals[i];
					// XXX
					// The evaluation of the Func creates a named function, but does not bind that function
					// in the local or global environment (the parser wraps this AST node in the VarSet node)
					// This means we cannot just look up the function as a local. But the semantics are such
					// that a reference to that variable in the enclosed scope (as a nonlocal) should be the
					// function (for things like recursive calls). So we search for that explicitly and give it
					// this function, going around the search through the local.
					if (Strings.equal(lid, id)) {
						vals[i] = (lid, fn);
					} else if (locals.has(lid)) {
						// get local
						vals[i] = (lid, locals[lid]);
					} else {
						throw(Exceptions.unboundLocal(lid));
						return value;
					}
				}
				value = fn;
			}
			Class(id, superclass, init, fields, methods) => {
				var i: CodeObject = if(init.isJust(), init.fromJust(), null);
				var s: ClassObject;
				match (superclass) {
					Just(e) => {
						var v = evalAST(e);
						if (isExn()) return value;
						
						assertType(ClassObjects.classClass, v);
						if (isExn()) return value;
						
						s = ClassObject.!(v);
					}
					Nothing => s = ClassObjects.classBase;
				}
				def f: Array<(string, CodeObject)> = fields;
				def m: Array<FuncObject> = Arrays.map(methods, funcToFunc);
				value = ClassObject.new(id, s, i, f, m);
			}
			Cond(cases) => {
				var sat = false;
				for (c in cases) {
					// case
					def cond = c.0, expr = c.1;

					// check cond
					def t = evalAST(cond);
					if (isExn()) return value;
					if (t.isTrue()) {
						def v = evalAST(expr);
						if (isExn()) return value;
						value = v;

						sat = true;
						break;
					}
				}

				if (!sat) {
					throw(Exceptions.noTrueBranch);
				}
			}
			While(cond, body) => {
				while (true) {
					def v = evalAST(cond);
					if (isExn()) return value;

					if (!v.isTrue()) {
						value = v;
						break;
					}

					evalAST(body);
					if (isExn()) return value;
				}
			}
			Begin(exprs) => {
				var last: Object;
				for (expr in exprs) {
					def v = evalAST(expr);
					if (isExn()) return value;

					last = v;
				}
				value = last;
			}
			GetField(field, expr) => {
				var obj = evalAST(expr);
				if (isExn()) return value;

				def v = obj.getField(field);
				if (v == null) {
					throw(Exceptions.missingField(obj, field));
					return value;
				} else {
					value = v;
				}
			}
			SetField(field, expr, newCode) => {
				var obj = evalAST(expr);
				if (isExn()) return value;

				var val = evalAST(newCode);
				if (isExn()) return value;

				if (obj.setField(field, val)) {
					value = val;
				} else {
					throw(Exceptions.missingField(obj, field));
					return value;
				}
			}
			New(expr) => {
				var cls = evalAST(expr);
				if (isExn()) return value;

				assertType(ClassObjects.classClass, cls);
				if (isExn()) return value;

				def clsObj = ClassObject.!(cls);
				def obj = Objects.newObject(clsObj);

				def classes: Vector<ClassObject> = Vector.new();
				for (cls = ClassObject.!(cls); cls != null; cls = cls.superclass)
					classes.put(cls);
			
				// initialization order is from base class downwards, which
				// may involve reinitializing some fields
				for (cls in classes.reverse().extract()) {
					// initialize fields
					for (field in cls.classFields) {
						def name = field.0, expr = field.1;

						// XXX hackfix
						if (expr == null) continue;
						// create new frames here!
						def frame = newFrame(Strings.format2("%s.%s", cls.name, name), expr, [], []);
						frame.eval();
						if (frame.isExn()) {
							rethrow(frame.getExn());
							return value;
						}
						obj.setField(name, frame.getValue());
					}
					// call initializer
					if (cls.init != null) {
						def args = [("self", obj)];
						def code = cls.init;

						// create new frames here!
						def frame = newFrame(Strings.format1("%s.init", cls.name), code, args, []);
						frame.eval();
						if (frame.isExn()) {
							rethrow(frame.getExn());
							return value;
						}
					} 
				}
				value = obj;
			}
			List(exprs) => {
				def vals: Vector<Object> = Vector.new();
				for (expr in exprs) {
					def v = evalAST(expr);
					if (isExn()) return value;

					vals.put(v);
				}
				
				def obj = ListObject.new();
				obj.list.puta(vals.extract());

				value = obj;
			}
		}

		return value;
	}
}

def funcToFunc(f: ASTData.Func) -> FuncObject {
	// XXX list for method
	return FuncObjects.newFO(f.id, f.params, [], f.body);
}

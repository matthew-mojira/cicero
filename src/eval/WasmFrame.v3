class WasmFrame extends FrameObject {
	new(prev: FrameObject, code: CodeObject, args: Array<(string, Object)>, nonlocalArgs: Array<(string, Object)>) super(prev, code, args) {
	}

	def var locals: Array<Object>;
	def var nonlocals: Array<Object>;

	def eval() {
		def f = Code.Wasm.!(code.code).wasm;
		match (f) {
			Func0(f) => result = Result.OK(f(this));
			Func1(f) => result = Result.OK(f(this, args[0].1));
			Func2(f) => result = Result.OK(f(this, args[0].1, args[1].1));
			Func3(f) => result = Result.OK(f(this, args[0].1, args[1].1, args[2].1));
			_ => {
				System.puts("unimplemented number of arguments\n");
				System.error("unimplemented", "WasmFrame.eval: FuncN");
			}
		}
	}

	def evalCode(code: CodeObject) -> Object {
		def f = Code.Wasm.!(code.code).wasm;
		match (f) {
			Func0(f) => {
				def value = f(this);
				result = Result.OK(value);
				return value;
			}
			_ => {
				// maybe should not happen?
				System.puts("unimplemented: WasmFrame.eval: FuncN (N != 0)\n");
				System.error("unimplemented", "WasmFrame.eval: FuncN (N != 0)");
			}
		}
		return null;
	}

	// TODO handle error condition
	def getGlobal(id: string) -> Object {
		if (globals.has(id))
			return globals[id];
		System.puts(Strings.format1("cicero exception: global %s not found\n", id));
		System.error("cicero exception", Strings.format1("global %s not found", id));
		return null;
	}

	def getNonlocal(idx: int) -> Object {
		return nonlocals[idx];
	}

	def setGlobal(id: string, obj: Object) -> Object {
		return globals[id] = obj;
	}
}

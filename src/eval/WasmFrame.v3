class WasmFrame extends FrameObject {
	new(prev: FrameObject, code: CodeObject, args: Array<(string, Object)>, nonlocals: Array<(string, Object)>) super(prev, code, args) {
	}

	def nonlocals: Array<(string, Object)>;

	def eval() {
		def f = Code.Wasm.!(code.code).wasm;
		match (f) {
			Func0(f) => result = f(this).data;
			Func1(f) => result = f(this, args[0].1).data;
			Func2(f) => result = f(this, args[0].1, args[1].1).data;
			Func3(f) => result = f(this, args[0].1, args[1].1, args[2].1).data;
			Func4(f) => result = f(this, args[0].1, args[1].1, args[2].1, args[3].1).data;
			Func5(f) => result = f(this, args[0].1, args[1].1, args[2].1, args[3].1, args[4].1).data;
			_ => {
				System.puts("unimplemented number of arguments\n");
				System.error("unimplemented", "WasmFrame.eval: FuncN");
			}
		}
	}

	def evalCode(code: CodeObject) -> Object {
		def f = Code.Wasm.!(code.code).wasm;
		match (f) {
			Func0(f) => {
				result = f(this).data;
				match (result) {
					OK(o) => return o;
					Exn(e) => return null;
					Uncomputed => {
						System.puts("evalCode in WasmFrame returned Uncomputed\n");
						System.error("internal error", "evalCode in WasmFrame returned Uncomputed\n");
					}
				}
			}
			_ => {
				// maybe should not happen?
				System.puts("unimplemented: WasmFrame.eval: FuncN (N != 0)\n");
				System.error("unimplemented", "WasmFrame.eval: FuncN (N != 0)");
			}
		}
		return null;
	}

	// TODO handle error condition
	def getGlobal(id: string) -> Object {
		if (globals.has(id))
			return globals[id];
		System.puts(Strings.format1("cicero exception: global %s not found\n", id));
		System.error("cicero exception", Strings.format1("global %s not found", id));
		return null;
	}

	def getNonlocal(id: string) -> Object { // FIXME don't use string
		for (p in nonlocals) {
			if (Strings.equal(p.0, id)) return p.1;
		}
		System.puts(Strings.format1("cannot find nonlocal %s\n", id));
		System.error("internal error", "cannot find nonlocal");
		return null;
	}

	def setGlobal(id: string, obj: Object) -> Object {
		return globals[id] = obj;
	}
}
